//// PostgreSQL Cache Table Generation
////
//// Generates migration files for PostgreSQL cache tables.
//// Called by the postgres:cache-table command.

import gleam/string
import glimr/console/console
import shellout
import simplifile

/// Generates a migration file for the cache table.
///
pub fn run(database: String, table: String) -> Nil {
  let migrations_path = "src/data/" <> database <> "/_migrations"

  // Ensure migrations directory exists
  let _ = simplifile.create_directory_all(migrations_path)

  // Generate timestamp for filename
  let timestamp = get_timestamp()
  let filename = timestamp <> "_create_cache_table.sql"
  let migration_path = migrations_path <> "/" <> filename

  // Generate the migration SQL (PostgreSQL uses BIGINT for expiration)
  let sql = "-- Generated by Glimr (postgres)

CREATE TABLE IF NOT EXISTS " <> table <> " (
    key TEXT PRIMARY KEY,
    value TEXT NOT NULL,
    expiration BIGINT NOT NULL
);

-- Index for faster expiration lookups
CREATE INDEX IF NOT EXISTS idx_" <> table <> "_expiration ON " <> table <> " (expiration);
"

  case simplifile.write(migration_path, sql) {
    Ok(_) -> {
      console.output()
      |> console.line_success("Generated: " <> migration_path)
      |> console.print()
    }
    Error(_) -> {
      console.output()
      |> console.line_error("Failed to write migration file")
      |> console.print()
    }
  }
}

/// Get current timestamp in YYYYMMDDHHMMSS format for migration
/// filenames.
///
fn get_timestamp() -> String {
  case shellout.command("date", ["+%Y%m%d%H%M%S"], ".", []) {
    Ok(output) -> string.trim(output)
    Error(_) -> "00000000000000"
  }
}
